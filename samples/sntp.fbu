type Timer component {
     fd FileDescriptor;
     Alarm push ();
};

init (this *Timer) Init (interval uint64) {
     this.fd = timerfd_create();
     if (interval != 0u64) {
          timerfd_settime(this.fd, interval);
     }
}

action (this +const * Timer) (readable(this.fd)) {
     activate Alarm () {
          var buf [8u]uint8;
          var r int = read(this.fd, buf[0u:8u]);
     }
}

type UdpMessage struct {
  host []uint8;
  port uint16;
  msg  []uint8;
};

func (this +foreign *UdpMessage) Copy () UdpMessage {
     var m UdpMessage;
     m.host = copy (this.host);
     m.port = this.port;
     m.msg = copy (this.msg);
     return m;
}

type Element struct
{
  value UdpMessage;
  next *Element;
};

type Queue struct
{
  head *Element;
  tail **Element;
};

func (this *Queue) Push (value UdpMessage)
{
  if this.head == nil
    {
      this.tail = &this.head;
    };

  var e *Element;
  e = new (Element);
  e.value = value;
  *this.tail = e;
  this.tail = &e.next;
}

func (this *Queue) Pop ()
{
  this.head = this.head.next;
  if this.head == nil
    {
      this.tail = &this.head;
    };
}

func (this +const * Queue) Empty () bool
{
  return this.head == nil;
}

func (this *Queue) Front () UdpMessage
{
  return this.head.value;
}

type UdpParticipant component {
     fd FileDescriptor;
     queue Queue;
};

reaction (this +const * UdpParticipant) Send (msg +foreign UdpMessage) {
     var m UdpMessage = msg.Copy ();
     activate {
          this.queue.Push (m);
     };
}

action (this +const * UdpParticipant) (!this.queue.Empty () && writable (this.fd)) {
       activate {
              var m UdpMessage = this.queue.Front ();
              sendto (this.fd, m.host, m.port, m.msg);
              this.queue.Pop ();
       }
}

action (this +const * UdpParticipant) (readable (this.fd)) {
       activate {
          var buf [128u]uint8;
          var r int = read(this.fd, buf[0u:128u]);
          println buf[0u:r];
       }
}

init (this *UdpParticipant) Init () {
     this.fd = udp_socket();
}

type System component {
     timer Timer;
     udp UdpParticipant;
     Send push (msg +foreign UdpMessage);
};

bind (this *System) {
     this.timer.Alarm -> this.Alarm;
     this.Send -> this.udp.Send;
}

init (this *System) Init () {
     this.timer.Init(1u64);
     this.udp.Init();
}

reaction (this +const * System) Alarm () {
     var msg +foreign UdpMessage;
     msg.host = `localhost`;
     msg.port = 7u16;
     msg.msg = `hello, world`;
     activate Send (msg) { };
}

instance s System Init;