// A component based on the Clock automaton of Distributed Algorithms, p. 213.
type Clock component {
  counter uint;
  flag bool;
  response port (t uint);
};

action (this $Clock) (true) {
  trigger {
    this.counter++;
    println `Counter is now `, this.counter;
  }
}

reaction (this $Clock) request () {
  trigger {
    this.flag = true;
  }
}

action (this $Clock) (this.flag) {
  trigger response (this.counter) {
    this.flag = false;
  }
}

type Sampler component {
  flag bool;
  request port ();
};

action (this $Sampler) (this.flag!) {
  trigger request () {
    this.flag = true;
  }
}

reaction (this $Sampler) response (t uint) {
  println `Received `, t;
  trigger {
    this.flag = false;
  }
}

type System component {
  sampler Sampler;
  clock Clock;
};

bind (this $System) {
  this.sampler.request -> this.clock.request;
  this.clock.response -> this.sampler.response;
}

instance s System;
