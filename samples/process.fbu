// From Distributed Algorithms, p. 204.

type Element struct
{
  value uint;
  next *Element;
};

type Queue struct
{
  head *Element;
  tail **Element;
};

func (this *Queue) Push (v uint)
{
  if this.head == nil
    {
      this.tail = &this.head;
    };

  var e *Element;
  e = new (Element);
  e.value = v;
  *this.tail = e;
  this.tail = &e.next;
}

func (this *Queue) Pop ()
{
  this.head = this.head.next;
  if this.head == nil
    {
      this.tail = &this.head;
    };
}

func (this +const * Queue) Empty () bool
{
  return this.head == nil;
}

func (this +const * Queue) Front () uint
{
  return this.head.value;
}

type Channel component
{
  queue Queue;
  receive push (message uint);
};

reaction (this +const * Channel) send (message uint)
{
  activate
  {
    this.queue.Push (message);
  };
}

action (this +const * Channel) (!this.queue.Empty ())
{
  activate receive (this.queue.Front ())
    {
      this.queue.Pop ();
    };
}

// From Distributed Algorithms, p. 205.

type Process component
{
  i uint;
  val [3u]uint;
  send [3u] push (v uint);
  decide push (v uint);
};

init (this *Process) Init (i uint)
{
  println (`I am process `, i);
  this.i = i;
}

reaction (this +const * Process) Initr (v uint)
{
  activate
  {
    this.val[this.i] = v;
    println (`Process `, this.i, ` initialized with value `, v);
  };
}

[3u] action (this +const * Process) (this.val[this.i] != 0u && this.i != IOTA)
{
  // send
  println (`Process `, this.i, ` sending `, this.val[this.i], ` to process `, IOTA);
  activate send[IOTA] (this.val[this.i])
  { };
}

[3u] reaction (this +const * Process) receive (v uint)
{
  println (`Process `, this.i, ` received `, v, ` from process `, IOTA);
  activate
  {
    this.val[IOTA] = v;
  };
}

func p (val [3u]uint) bool {
  var i uint;
  i = 0u;
  while i != 3u {
    if val[i] == 0u {
      return false;
    };
    i++;
  };

  return true;
}

func f (val [3u]uint) uint
{
  var i, sum uint;
  i = 0u;
  sum = 0u;
  while i != 3u {
    sum += val[i];
    i++;
  };
  return sum;
}

action (this +const * Process) (p (this.val))
{
  // decide
  var v uint;
  v = f (this.val);
  println (`Process `, this.i, ` decided the value is `, v);
  activate decide (v)
  { };
}

type System component
{
  process [3u]Process;
  channel [3u][3u]Channel;
  initp [3u] push (v uint);
};

init (this *System) Init ()
{
  var i uint;
  i = 0u;
  while i != 3u {
    this.process[i].Init (i);
    i++;
  };
}

[3u] action (this +const * System) (true)
{
  // send
  activate initp[IOTA] (100u + IOTA)
  { };
}

[3u] reaction (this +const * System) decide (v uint)
{
  println (`Process `, IOTA, ` decided the value is `, v);
}

bind (this *System) {
  for i .. 3u {
    this.initp[i] -> this.process[i].Initr;
    this.process[i].decide -> this.decide .. i;

    for j .. 3u {
      if i != j {
        this.process[i].send[j] -> this.channel[i][j].send;
        this.channel[i][j].receive -> this.process[j].receive .. i;
      }
    }
  }
}

instance system System Init;
