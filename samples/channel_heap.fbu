type Element struct
{
  value @heap uint;
  next @Element;
};

type Queue struct
{
  head @Element;
  tail @@Element;
};

func (this @Queue) Push (v @heap uint)
{
  if this.head == nil
    {
      this.tail = this.head&;
    };

  var e @Element;
  e = new Element;
  e.value = v;
  this.tail@ = e;
  this.tail = e.next&;
}

func (this @Queue) Pop ()
{
  this.head = this.head.next;
  if this.head == nil
    {
      this.tail = this.head&;
    };
}

func (this $Queue) Empty () bool
{
  return this.head == nil;
}

func (this $Queue) Front () $heap uint
{
  return this.head.value;
}

type Channel component
{
  queue Queue;
  receive port (message ?heap uint);
};

reaction (this $Channel) send (message ?heap uint)
{
  var x @heap uint;
  x = move (message);
  trigger
  {
    this.queue.Push (x);
  };
}

action (this $Channel) (this.queue.Empty ()!)
{
  trigger receive (this.queue.Front ())
    {
      this.queue.Pop ();
    };
}

type System component
{
  channel Channel;
  send_count uint;
  send port (message ?heap uint);
};

func (this @System) Initially ()
{ }

action (this $System) (this.send_count != 100u)
{
  var x @heap uint;
  x = new heap uint;
  change (x, y @uint)
  {
    y@ = this.send_count;
  };
  trigger send (x)
  {
    println `Sent `, this.send_count;
    this.send_count++;
  };
}

reaction (this $System) receive (message ?heap uint)
{
  var x @uint;
  x = merge (message);
  println `Received `, x@;
}

bind (this @System) {
  this.send -> this.channel.send;
  this.channel.receive -> this.receive;
}

instance s System Initially;
