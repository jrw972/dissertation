// A version of the Clock automaton of Distributed Algorithms, p. 213.
// This implementation uses synchronous getters to increase parallelism.

type Counter component {
  counter uint;
};

action (this *Counter const) (true) {
  trigger {
    this.counter++;
    println `Counter is now `, this.counter;
  };
}

func (this *Counter const) Counter () uint {
  return this.counter;
}

type Clock component {
  counter Counter;
  flag bool;
  response push (t uint);
};

reaction (this *Clock const) request () {
  trigger {
    this.flag = true;
  };
}

action (this *Clock const) (this.flag) {
  trigger response (this.counter.Counter()) {
    this.flag = false;
  };
}

type Sampler component {
  flag bool;
  request push ();
};

action (this *Sampler const) (!this.flag) {
  trigger request () {
    this.flag = true;
  };
}

reaction (this *Sampler const) response (t uint) {
  println `Received `, t;
  trigger {
    this.flag = false;
  };
}

type System component {
  sampler Sampler;
  clock Clock;
};

init (this *System) Initially ()
{ }

bind (this *System) {
  this.sampler.request -> this.clock.request;
  this.clock.response -> this.sampler.response;
}

instance s System Initially;
